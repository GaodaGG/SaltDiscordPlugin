plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.kapt' version '2.0.21'
}

group = 'com.gg.SaltDiscordPlugin'
version = '0.0.6'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    // https://mvnrepository.com/artifact/software.amazon.awssdk/s3
    implementation 'software.amazon.awssdk:s3:2.32.30'
    compileOnly 'net.jthink:jaudiotagger:3.0.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.JnCrMx:discord-game-sdk4j:v1.0.0'
//    implementation 'net.java.dev.jna:jna:5.13.0'
//    implementation 'net.java.dev.jna:jna-platform:5.13.0'
    compileOnly "com.github.Moriafly:spw-workshop-api:0.1.0-dev10"
    kapt "com.github.Moriafly:spw-workshop-api:0.1.0-dev10"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

def pluginClass = "com.gg.SaltDiscordPlugin.MainPlugin"
def pluginId = "com.gg.SaltDiscordPlugin"
def pluginName = "Discord 丰富状态"
def pluginVersion = version
def pluginDescription = "在Discord中显示 SPW 丰富状态"
def pluginProvider = "GaodaGG & zmxlsss666"
def pluginOpenSourceUrl = "https://github.com/GaodaGG/SaltDiscordPlugin"

tasks.named("jar") {
    destinationDirectory = file(System.getenv("APPDATA") + "/Salt Player for Windows/workshop/plugins/")
    archiveFileName = "plugin-${pluginName}-${pluginVersion}.jar"

    manifest {
        attributes["Plugin-Class"] = pluginClass
        attributes["Plugin-Id"] = pluginId
        attributes["Plugin-Name"] = pluginName
        attributes["Plugin-Version"] = pluginVersion
        attributes["Plugin-Description"] = pluginDescription
        attributes["Plugin-Provider"] = pluginProvider
        attributes["Plugin-Open-Source-Url"] = pluginOpenSourceUrl
        attributes["Plugin-Has-Config"] = true
    }
}

tasks.register("plugin", Jar) {
    destinationDirectory = file(System.getenv("APPDATA") + "/Salt Player for Windows/workshop/plugins/")
    archiveFileName = "plugin-${pluginId}-${pluginVersion}.zip"
    archiveBaseName.set("plugin-" + pluginId + "-" + pluginVersion)

    into("classes") {
        with(tasks.named("jar").get())
    }
    dependsOn(configurations.runtimeClasspath)
    into("lib") {
        from({
            configurations.runtimeClasspath
                    .filter { it.name.endsWith("jar") }
        })
    }
    archiveExtension = 'zip'
}